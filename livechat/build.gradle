plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

def artifactId = 'desk360-livechat-android-sdk'
def groupId = 'com.github.Teknasyon-Teknoloji'
def versionN = "1.2.2"

android {
    namespace 'com.desk360.livechat'

    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/licenses/**']
        }
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'META-INF/licenses/**']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    api 'androidx.core:core-ktx:1.5.0'
    api 'androidx.appcompat:appcompat:1.3.0'
    api 'com.google.android.material:material:1.3.0'
    api 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Retrofit
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api "com.squareup.retrofit2:converter-gson:2.9.0"
    api "com.squareup.okhttp3:logging-interceptor:4.9.1"
    api 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'

    // RxJava
    api "io.reactivex.rxjava2:rxandroid:2.1.1"

    // lifecycle
    def lifecycle_version = "2.5.1"

    // ViewModel
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // Lifecycles only (without ViewModel or LiveData)
    api "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // glide
    api 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // firebase
    api 'com.google.firebase:firebase-messaging-ktx:23.0.7'
    api 'com.google.firebase:firebase-analytics-ktx:21.1.0'
    api "com.google.firebase:firebase-common-ktx:20.1.1"
    api "com.google.firebase:firebase-database-ktx:20.0.5"
    api 'com.google.firebase:firebase-auth-ktx:21.0.7'

    //emoji
    api 'com.vanniktech:emoji-google:0.7.0'

    // balloon
    api "com.github.skydoves:balloon:1.3.6"

    def room_version = "2.4.3"
    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    api "androidx.room:room-rxjava2:$room_version"
    api "androidx.room:room-guava:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                from components.release
                setGroupId groupId
                setArtifactId artifactId
                version versionN
            }
        }
    }
}